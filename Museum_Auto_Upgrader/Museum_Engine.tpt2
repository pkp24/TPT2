:name D0S.Museum v2.0.0:Engine

:global string CurrentElement
:global int CurrentBuyLevel
:global int MaxBuyLevel
:global int MaxCraftLevel
:global int Position
:global int Toggle

; Engage turbo exec v2 (if it is present)
executesync("turbo start")

;Clear buying area for a fresh start

Position = -1
NextPosition:
  clear("inventory")
  Position += 1
  CurrentElement = element("equipped", Position)
  goto(if(Position > 200,end,if(CurrentElement == "",NextPosition,Continue)))
  Continue:
  buyMarket(CurrentElement, min(MaxCraftLevel, MaxBuyLevel))
  CurrentBuyLevel = tier("inventory",0)

delete(if(CurrentBuyLevel > tier("equipped",Position),"equipped",""),Position)

;Skip it if you can never get better than it

gotoif(NextPosition,\
  CurrentBuyLevel != -1 && CurrentBuyLevel + 11 <= tier("equipped",Position) ||\
  MaxCraftLevel <= tier("equipped",Position))
executesync(if(\
  CurrentBuyLevel <= 1,\
  "D0S.Museum v2.0.0:NormalBuyer",\
  "D0S.Museum v2.0.0:MarketBuyer"\
))

gotoif(NextPosition, Position < 200 && Toggle == 1)

; All jumps must come here, to ensure turbo is stopped.
end:
executesync("turbo stop")
; Need one instruction at the end that is safe to repeat
Toggle = 0
